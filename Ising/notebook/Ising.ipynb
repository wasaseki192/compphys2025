{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "13bbf290",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "# スピン配位の初期化\n",
    "function spins_initialize(Lx, Ly, rng)\n",
    "    return rand(rng, [-1, 1], Lx, Ly)\n",
    "end\n",
    "\n",
    "# Siの計算\n",
    "function Si_calculation(Ck, ix, iy, Lx, Ly)\n",
    "    jx = ix + 1\n",
    "    if jx > Lx\n",
    "        jx -= Lx\n",
    "    end\n",
    "    jy = iy\n",
    "    Si = Ck[jx, jy]\n",
    "\n",
    "    jx = ix - 1\n",
    "    if jx < 1\n",
    "        jx += Lx\n",
    "    end\n",
    "    jy = iy\n",
    "    Si += Ck[jx, jy]\n",
    "\n",
    "    jx = ix\n",
    "    jy = iy + 1\n",
    "    if jy > Ly\n",
    "        jy -= Ly\n",
    "    end\n",
    "    Si += Ck[jx, jy]\n",
    "\n",
    "    jx = ix\n",
    "    jy = iy - 1\n",
    "    if jy < 1\n",
    "        jy += Ly\n",
    "    end\n",
    "    Si += Ck[jx, jy]\n",
    "\n",
    "    return Si\n",
    "end\n",
    "\n",
    "# エネルギーの計算\n",
    "function E_measure(Ck, J, h, Lx, Ly)\n",
    "    E = 0\n",
    "    for iy = 1:Ly\n",
    "        for ix = 1:Lx\n",
    "            Si = Si_calculation(Ck, ix, iy, Lx, Ly)\n",
    "            σi = Ck[ix, iy]\n",
    "            E += -(J/2)*σi*Si - h*σi\n",
    "        end\n",
    "    end\n",
    "    return E\n",
    "end\n",
    "\n",
    "# エネルギー差の計算\n",
    "function ΔE_calculation(Ck, J, h, ix, iy, Lx, Ly)\n",
    "    Si = Si_calculation(Ck, ix, iy, Lx, Ly)\n",
    "    return 2J*Ck[ix, iy]*Si + 2h*Ck[ix, iy]\n",
    "end\n",
    "\n",
    "# メトロポリスモンテカルロ法によるスピンの更新\n",
    "function spins_update(Ck, ix, iy, J, h, T, Lx, Ly, rng)\n",
    "    ΔE = ΔE_calculation(Ck, J, h, ix, iy, Lx, Ly)\n",
    "    σi = Ck[ix, iy]\n",
    "    σ_new = ifelse(rand(rng)≤exp(-ΔE/T), -σi, σi)\n",
    "    return σ_new\n",
    "end\n",
    "\n",
    "\n",
    "# メトロポリスモンテカルロ法の実行，各種物理量の計算\n",
    "function Ising_Montecarlo(step_equilibration, step_MC, measure_interval, J, h, T, Lx, Ly)\n",
    "    rng = MersenneTwister(123)  # 乱数の種\n",
    "    count_measure = 0  # 測定回数\n",
    "    E_mean = 0  # エネルギーの時間平均\n",
    "    E2_mean = 0  # エネルギーの2乗の時間平均\n",
    "    m_mean = 0  # 磁化の時間平均\n",
    "    absm_mean = 0  # 磁化の絶対値の時間平均\n",
    "\n",
    "    Ck = spins_initialize(Lx, Ly, rng)\n",
    "\n",
    "    for trial = 1:step_equilibration+step_MC\n",
    "        for ix = 1:Lx\n",
    "            for iy = 1:Ly\n",
    "                Ck[ix, iy] = spins_update(Ck, ix, iy, J, h, T, Lx, Ly, rng)\n",
    "            end\n",
    "        end\n",
    "\n",
    "        if trial > step_equilibration && trial % measure_interval == 0\n",
    "            count_measure += 1\n",
    "            E = E_measure(Ck, J, h, Lx, Ly) / (Lx*Ly)\n",
    "            E_mean += E\n",
    "            E2_mean += E^2\n",
    "            m = sum(Ck) / (Lx*Ly)  # 磁化の計算\n",
    "            m_mean += m\n",
    "            absm_mean += abs(m)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    E_mean /= count_measure\n",
    "    E2_mean /= count_measure\n",
    "    m_mean /= count_measure\n",
    "    absm_mean /= count_measure\n",
    "    C_mean = (E2_mean-E_mean^2) / T^2\n",
    "\n",
    "    return E_mean, m_mean, absm_mean, C_mean\n",
    "end\n",
    "\n",
    "# 上記の設定でシミュレーションを実行\n",
    "function test()\n",
    "    step_equilibration = 10000  # 熱平衡に達するまでのステップ数\n",
    "    step_MC = 50000  # 時間平均計算に用いるステップ数\n",
    "    measure_interval = 10  # 数値の測定間隔\n",
    "    J = 1  # 交換相互作用定数\n",
    "    h = 0  # 外部磁場\n",
    "    Ts = range(0.01, 5, length=200)  # 温度\n",
    "    Lx = 50  # 格子のサイズ\n",
    "    Ly = 50\n",
    "    E_T = []  # 各温度におけるエネルギー\n",
    "    m_T = []  # 各温度における磁化\n",
    "    absm_T = []  # 各温度における磁化の絶対値\n",
    "    C_T = []  # 各温度における比熱\n",
    "\n",
    "    # 各温度で各種物理量を計算，記録\n",
    "    for T in Ts\n",
    "        E_mean, m_mean, absm_mean, C_mean = Ising_Montecarlo(step_equilibration, step_MC, measure_interval, J, h, T, Lx, Ly)\n",
    "        push!(E_T, E_mean)\n",
    "        push!(m_T, m_mean)\n",
    "        push!(absm_T, absm_mean)\n",
    "        push!(C_T, C_mean)\n",
    "    end\n",
    "\n",
    "    # グラフ描画\n",
    "    plot(Ts, E_T, label=\"\", xlabel=L\"k_{\\textrm{B}}T\", ylabel=\"Energy\", title=\"Temperature Dependence of Energy\")\n",
    "    savefig(\"E_Tdep.png\")\n",
    "    plot(Ts, m_T, label=\"\", xlabel=L\"k_{\\textrm{B}}T\", ylabel=\"Magnetization\", title=\"Temperature Dependence of Magnetization\")\n",
    "    savefig(\"m_Tdep.png\")\n",
    "    plot(Ts, absm_T, label=\"\", xlabel=L\"k_{\\textrm{B}}T\", ylabel=\"Absolute Value of Magnetization\", title=\"Temperature Dependence of Magnetization\")\n",
    "    savefig(\"absm_Tdep.png\")\n",
    "    plot(Ts, C_T, label=\"\", xlabel=L\"k_{\\textrm{B}}T\", ylabel=\"Specific Heat\", title=\"Temperature Dependence of Specific Heat\")\n",
    "    savefig(\"C_Tdep.png\")\n",
    "end\n",
    "\n",
    "test()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
